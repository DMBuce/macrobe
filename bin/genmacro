#!/usr/bin/perl

use strict;
use warnings;

# read config
my $file = "$ENV{HOME}/.config/macrobe/config.ini";
my $config = {};
my $sec = "general";
open(my $fh, "<", $file) || die $!;
while (my $line = <$fh>) {
	chomp $line;

	# strip trailing comments
	$line =~ s/\s+[#;].*$//;
	# strip full-line comments
	$line =~ s/^\s*[#;].*//;
	# skip comments and empty lines
	next if $line eq "";

	# process section headers
	if ($line =~ /^\[(.*)\]$/) {
		$sec = $1;
	# process key-value pairs
	} else {
		# normalize lines without a '=' separator
		if (not $line =~ /=/) {
			# turn 'nokey' into 'key=0'
			if ($line =~ /^[Nn][Oo]/) {
				$line .= "=0"
			# turn 'key' into 'key=1'
			} else {
				$line .= "=1"
			}
		}

		# split line
		my ($key, $value) = split(/=/, $line, 2);

		# remove leading and trailing whitespace
		$key =~ s/^\s+//;
		$key =~ s/\s+$//;
		$value =~ s/^\s+//;
		$value =~ s/\s+$//;

		# interpret ~/ as $HOME/
		$value =~ s/^~\//$ENV{HOME}\//;

		# store key and value
		$config->{$sec}{$key} = $value;
	}
}
close($fh);

# process escape sequences in config
if ($config->{genmacro}{'\\'} =~ /^=\s/) {
	$config->{genmacro}{'='} = $config->{genmacro}{'\\'};
	$config->{genmacro}{'='} =~ s/^=\s+//;
}
for my $key (keys %{$config->{genmacro}}) {
	if ($key eq "\\n") {
		$config->{genmacro}{"\n"} = $config->{genmacro}{"\\n"};
	} elsif ($key eq "\\t") {
		$config->{genmacro}{"\t"} = $config->{genmacro}{"\\t"};
	} elsif ($key eq "\\s") {
		$config->{genmacro}{" "} = $config->{genmacro}{"\\s"};
	} elsif (substr($key, 0, 1) eq "\\" and length($key) > 1) {
		my $k = substr($key, 1);
		$config->{genmacro}{$k} = $config->{genmacro}{$key};
	}
}

# generate macro
print("Delay $config->{genmacro}{start_delay}\n") if $config->{genmacro}{start_delay};
while (<>) {
	foreach my $c (split //, $_) {
		if ($config->{genmacro}{$c} and $config->{genmacro}{$c} =~ /(Shift_.) (.*)/) {
			print("KeyStrPress $1\n");
			print("KeyStrPress $2\n");
			print("KeyStrRelease $2\n");
			print("KeyStrRelease $1\n");
		} elsif ($config->{genmacro}{$c}) {
			print("KeyStrPress $config->{genmacro}{$c}\n");
			print("KeyStrRelease $config->{genmacro}{$c}\n");
		} else {
			print("KeyStrPress $c\n");
			print("KeyStrRelease $c\n");
		}
		print("\n") if ($c eq "\n");
	}
}

